{
  config,
  options,
  pkgs,
  lib,
  ...
}:

let
  cfg = config.hardware.asahi;
in
{
  imports = [
    ./kernel
    ./peripheral-firmware
    ./boot-m1n1
    ./sound
  ];

  config = lib.mkIf cfg.enable {
    nixpkgs.overlays = lib.mkIf (cfg.overlay != null) (lib.mkBefore [ cfg.overlay ]);

    hardware.asahi.pkgs =
      if cfg.pkgsSystem != "aarch64-linux" then
        import (pkgs.path) {
          crossSystem.system = "aarch64-linux";
          localSystem.system = cfg.pkgsSystem;
          overlays = [ cfg.overlay ];
        }
      else
        pkgs;

    # 900 is higher priority than mkDefault but lower than just setting
    hardware.sensor.iio.enable = lib.mkOverride 900 true;
  };

  options.hardware.asahi = {
    enable = lib.mkOption {
      type = lib.types.bool;
      apply =
        v:
        lib.warnIf (options.hardware.asahi.enable.highestPrio == (lib.mkOptionDefault { }).priority) ''
          You're currently relying on `hardware.asahi.enable` to default to true.

          This will change in the future, to allow including the module unconditionally,
          but explicitly enable on Asahi machines.

          Please explicitly set `hardware.asahi.enable = true;`, to avoid this
          suddenly being disabled in the future.
        '' v;
      default = true;
      description = ''
        Enable the basic Asahi Linux components, such as kernel and boot setup.
      '';
    };

    pkgsSystem = lib.mkOption {
      type = lib.types.str;
      default = "aarch64-linux";
      description = ''
        System architecture that should be used to build the major Asahi
        packages, if not the default aarch64-linux. This allows installing from
        a cross-built ISO without rebuilding them during installation.
      '';
    };

    pkgs = lib.mkOption {
      type = lib.types.raw;
      description = ''
        Package set used to build the major Asahi packages. Defaults to the
        ambient set if not cross-built, otherwise re-imports the ambient set
        with the system defined by `hardware.asahi.pkgsSystem`.
      '';
    };

    overlay = lib.mkOption {
      type = lib.types.nullOr (
        lib.mkOptionType {
          name = "nixpkgs-overlay";
          description = "nixpkgs overlay";
          check = lib.isFunction;
          merge = lib.mergeOneOption;
        }
      );
      default = import ../packages/overlay.nix;
      defaultText = "overlay provided with the module";
      description = ''
        The nixpkgs overlay for asahi packages.
      '';
    };
  };
}
